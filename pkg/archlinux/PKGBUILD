# Maintainer: Benjamin Maisonnas <ben@wainei.net>

pkgname=twitchcancer
pkgver=0.1.4
pkgrel=1
pkgdesc="Python suite of tools targeted at diagnosing, monitoring and recording data about chat cancer on Twitch."
arch=(any)
url="https://github.com/Benzhaomin/twitchcancer/"
license=('GPL3')
groups=()
depends=('python' 'python-pyzmq' 'python-yaml')
makedepends=('python' 'python-setuptools')
checkdepends=('python-tox' 'python-pytest')
optdepends=('python-autobahn: monitor chat using web sockets and expose a web socket API'
            'python-irc: monitor chat using the IRC protocol'
            'python-pymongo: record/persist cancer levels')
provides=()
conflicts=()
replaces=()
backup=('etc/twitchcancer/twitchcancer.yml' 'etc/logrotate.d/twitchcancer')
options=('emptydirs')
install=twitchcancer.install
source=("https://github.com/Benzhaomin/$pkgname/archive/$pkgver.zip"
        'twitchcancer.install'
        'twitchcancer-monitor.service'
        'twitchcancer-record.service'
        'twitchcancer-expose.service'
        'twitchcancer.yml'
        'twitchcancer.logrotate.d')
sha256sums=('48e83453519364ee09bada021cbe4abd359e84f272109e5f662f886f0cd3867b'
            'fb4fdb25d8485f5e74efb592f5f303a5dbbf045673b9c86b264154a4007796a3'
            '67b1fc9a85466903419f38c66f7ee65398662ff0bff1c47b49f9fc80fde7b219'
            '53f9082c8b125bd0004aa137be5c3df5d8f788f0368e429e5a863aa53ed799af'
            '1e8920b2242bf476c0ac1621a2a77f2aece2605a7a4d79533116abdb5857f053'
            'eb3eaf26b038e51e5ed2a6bee3bb248a088e3b1e2b52da0e9907bb07dbd044b9'
            '4a66a7f99dcec62410afb27bfb8bb158d1d9c99f08fd1ceb5137973e732c7fd3')

check() {
  cd "$srcdir/$pkgname-$pkgver"
  PYTHONPATH=:`pwd`$PYTHONPATH tox
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # python package
  python setup.py install --root="$pkgdir/" --optimize=1

  # logs
  install -dm755 "$pkgdir/var/log/twitchcancer"
  install -D -m644 "$srcdir/twitchcancer.logrotate.d" "$pkgdir/etc/logrotate.d/twitchcancer"

  # config file
  install -D -m644 "$srcdir/twitchcancer.yml" "$pkgdir/etc/twitchcancer/twitchcancer.yml"

  # services
  install -D -m644 "${srcdir}/twitchcancer-monitor.service" "${pkgdir}/usr/lib/systemd/system/twitchcancer-monitor.service"
  install -D -m644 "${srcdir}/twitchcancer-record.service" "${pkgdir}/usr/lib/systemd/system/twitchcancer-record.service"
  install -D -m644 "${srcdir}/twitchcancer-expose.service" "${pkgdir}/usr/lib/systemd/system/twitchcancer-expose.service"
}
